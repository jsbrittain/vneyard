"""Filter data based on a list of criteria

This script reads a CSV file and filters it based on a list of criteria. The list of criteria are provided as dictionaries in the config file.

Params:
    Source (str): "data.csv"
    Filters (list): List of criteria to filter the data by. For example, to filter by a CountryCode of "GB", the list would be ["CountryCode": "GB"]
"""
configfile: "config/config.yaml"
from grapevne_helper import import_grapevne
import json

grapevne = import_grapevne(workflow)
globals().update(vars(grapevne))


rule target:
    input:
        source=input(params("Source")),
        script=script("filter.py"),
    output:
        output(params("Source")),
    log:
        log("test.log")
    params:
        filters=json.dumps(params("Filters")).replace("\"", "\'"),
    conda:
        env("conda.yaml")
    shell:
        """
        python {input.script} \
            --source="{input.source}" \
            --dest="{output}" \
            --filters="{params.filters}" \
        """

rule _test:
    input:
        source=input(params("Source")),
        filtered=output(params("Source")),
    run:
        # Test data format:
        #  Date, CountryCode, CountryName, ...

        # Source file should have 10 lines (plus one header)
        with open(input.source) as f:
            assert len(f.readlines()) == 11

        # Filtered file should have 1 line (plus one header)
        with open(input.filtered) as f:
            lines = f.readlines()
        assert len(lines) == 2
        assert lines[-1].split(',')[1] == "GB"  # CountryCode should be filtered to 'GB'
